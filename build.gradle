plugins {
    id "io.github.der-fruhling.packmaker" version "1.0.0-SNAPSHOT"
}

packageMetadata {
    version = "1.0.0"
    name = "Example Modpack"

    summary = """
        This is an example project.
    """.stripIndent()
}

def resourcePackZip = tasks.register("resourcePackZip", Zip) {
    from("src/resources/*")
    
    archiveBaseName = project.name
    archiveClassifier = "resources"
}

def dataPackZip = tasks.register("dataPackZip", Zip) {
    from("src/data/*")

    archiveBaseName = project.name
    archiveClassifier = "datapack"
}

dependencies {
    // The version of the game the modpack uses.
    minecraft "com.mojang:minecraft:1.20.4"
    
    // The modloader to include, if any
    // (technically it is possible to omit this to get just the vanilla game)
    minecraft "net.fabricmc:fabric-loader:0.15.9"
    
    // The following minecraft items can also be used in place of fabric-loader above:
    //minecraft "net.neoforged:neoforge:20.4.223"
    //minecraft "net.minecraftforge:forge:49.0.39"
    //minecraft "org.quiltmc:quilt-loader:0.24.0"

    // The format of these is
    // > mod "maven.modrinth:<project id>:<version number>"
    //
    // This information can be found in the URL for the version you want to include.
    //
    // e.g. https://modrinth.com/mod/fabric-api/version/0.96.11+1.20.4
    //                               ^^^^^^^^^^         ^^^^^^^^^^^^^^
    //                               project id         version number
    mod "maven.modrinth:fabric-api:0.96.11+1.20.4"
    // Ensure you are using the correct version for your modloader.
    // Version numbers are NOT consistant between mods, the mod developer can put
    // whatever they want in there, including the target game version or not.
    // Pack installers will almost certainly not correct you!
    
    // Performance mods for warp speed
    mod "maven.modrinth:lithium:mc1.20.4-0.12.1"
    
    // These mods will only be installed on the client
    modClient "maven.modrinth:sodium:mc1.20.4-0.5.8"
    modClient "maven.modrinth:reeses-sodium-options:mc1.20.4-1.7.2"
    modClient "maven.modrinth:iris:1.6.17+1.20.4"
    modClient "maven.modrinth:immediatelyfast:1.2.11+1.20.4-fabric"
    
    // This mod will only be installed to a dedicated server.
    modDedicatedServer "maven.modrinth:c2me-fabric:0.2.0+alpha.11.65+1.20.4"
    
    // at the moment, things like resource packs and data packs can only be
    // specified by file
    resourcePack files(resourcePackZip)
    dataPack files(dataPackZip)
    // data packs are installed to <world name>/datapacks, where <world name>
    // is configured below
}

// with this line data packs will be installed to something-else/datapacks
// does not update server.properties (see server-overrides)
packageModrinth {
    worldName = "something-else"
}
